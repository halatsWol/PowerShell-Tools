; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "PowerShell-Tools - Modules Suite"
#define MyAppVersion "v1.4"
#define MyAppPublisher "Marflow Software"
#define MyAppURL "https://www.kMarflow.com/"
#define BaseDir "{userdocs}\WindowsPowerShell\Modules"
#define PS7Dir "{userdocs}\PowerShell\Modules"

[Setup]
; NOTE: The value of AppId uniquely identifies this application. Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
PrivilegesRequired=lowest
AppId={{1A25E73F-95C7-437F-A1B6-C30B03D47142}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
AppVerName={#MyAppName}, {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppCopyright=Copyright (C) 2025 {#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={autopf}\PowerShellToolsSuite
CreateAppDir=no
LicenseFile={#SourcePath}\LICENSE
InfoBeforeFile={#SourcePath}\Pre-Install.nfo
InfoAfterFile={#SourcePath}\Post-Install.nfo
OutputDir={#SourcePath}\installer
OutputBaseFilename=Setup_Powershell-ModulesSuite_{#MyAppVersion}_UserInstaller
UninstallFilesDir={userappdata}\{#MyAppPublisher}\{#MyAppName}\{#MyAppVersion}\uninst
Compression=lzma
SolidCompression=yes
WizardStyle=modern
AllowNetworkDrive=no
AllowUNCPath=no

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Dirs]
; PS 5.1
Name: "{#BaseDir}\TempDataCleanup"
Name: "{#BaseDir}\RepairSystem"
Name: "{#BaseDir}\Shortcuts"
Name: "{#BaseDir}\CredentialHandler"
; PS 7
Name: "{#PS7Dir}\TempDataCleanup"
Name: "{#PS7Dir}\RepairSystem"
Name: "{#PS7Dir}\Shortcuts"
Name: "{#PS7Dir}\CredentialHandler"
; uninst
Name: "{userappdata}\{#MyAppPublisher}\{#MyAppName}\{#MyAppVersion}\uninst"

[Files]
; TempDataCleanup
Source: "{#SourcePath}\modules\TempDataCleanup\TempDataCleanup.psm1"; DestDir: "{#BaseDir}\TempDataCleanup"; Flags: ignoreversion
Source: "{#SourcePath}\modules\TempDataCleanup\TempDataCleanup.psd1"; DestDir: "{#BaseDir}\TempDataCleanup"; Flags: ignoreversion
Source: "{#SourcePath}\modules\TempDataCleanup\en-US\*"; DestDir: "{#BaseDir}\TempDataCleanup\en-US"; Flags: ignoreversion recursesubdirs createallsubdirs
; PS7Dir
Source: "{#SourcePath}\modules\TempDataCleanup\TempDataCleanup.psm1"; DestDir: "{#PS7Dir}\TempDataCleanup"; Flags: ignoreversion
Source: "{#SourcePath}\modules\TempDataCleanup\TempDataCleanup.psd1"; DestDir: "{#PS7Dir}\TempDataCleanup"; Flags: ignoreversion
Source: "{#SourcePath}\modules\TempDataCleanup\en-US\*"; DestDir: "{#PS7Dir}\TempDataCleanup\en-US"; Flags: ignoreversion recursesubdirs createallsubdirs

; RepairSystem
Source: "{#SourcePath}\modules\Repair-System\RepairSystem.psd1"; DestDir: "{#BaseDir}\RepairSystem"; Flags: ignoreversion
Source: "{#SourcePath}\modules\Repair-System\RepairSystem.psm1"; DestDir: "{#BaseDir}\RepairSystem"; Flags: ignoreversion
Source: "{#SourcePath}\modules\Repair-System\en-US\*"; DestDir: "{#BaseDir}\RepairSystem\en-US"; Flags: ignoreversion recursesubdirs createallsubdirs
; PS7Dir
Source: "{#SourcePath}\modules\Repair-System\RepairSystem.psd1"; DestDir: "{#PS7Dir}\RepairSystem"; Flags: ignoreversion
Source: "{#SourcePath}\modules\Repair-System\RepairSystem.psm1"; DestDir: "{#PS7Dir}\RepairSystem"; Flags: ignoreversion
Source: "{#SourcePath}\modules\Repair-System\en-US\*"; DestDir: "{#PS7Dir}\RepairSystem\en-US"; Flags: ignoreversion recursesubdirs createallsubdirs

; Shortcuts
Source: "{#SourcePath}\modules\Shortcuts\Shortcuts.psm1"; DestDir: "{#BaseDir}\Shortcuts"; Flags: ignoreversion
Source: "{#SourcePath}\modules\Shortcuts\Shortcuts.psd1"; DestDir: "{#BaseDir}\Shortcuts"; Flags: ignoreversion
Source: "{#SourcePath}\modules\Shortcuts\en-US\*"; DestDir: "{#BaseDir}\Shortcuts\en-US"; Flags: ignoreversion recursesubdirs createallsubdirs
; PS7Dir
Source: "{#SourcePath}\modules\Shortcuts\Shortcuts.psm1"; DestDir: "{#PS7Dir}\Shortcuts"; Flags: ignoreversion
Source: "{#SourcePath}\modules\Shortcuts\Shortcuts.psd1"; DestDir: "{#PS7Dir}\Shortcuts"; Flags: ignoreversion
Source: "{#SourcePath}\modules\Shortcuts\en-US\*"; DestDir: "{#PS7Dir}\Shortcuts\en-US"; Flags: ignoreversion recursesubdirs createallsubdirs

; CredentialHandler
Source: "{#SourcePath}\modules\CredentialHandler\CredentialHandler.psm1"; DestDir: "{#BaseDir}\CredentialHandler"; Flags: ignoreversion
Source: "{#SourcePath}\modules\CredentialHandler\CredentialHandler.psd1"; DestDir: "{#BaseDir}\CredentialHandler"; Flags: ignoreversion
Source: "{#SourcePath}\modules\CredentialHandler\en-US\*"; DestDir: "{#BaseDir}\CredentialHandler\en-US"; Flags: ignoreversion recursesubdirs createallsubdirs
; PS7Dir
Source: "{#SourcePath}\modules\CredentialHandler\CredentialHandler.psm1"; DestDir: "{#PS7Dir}\CredentialHandler"; Flags: ignoreversion
Source: "{#SourcePath}\modules\CredentialHandler\CredentialHandler.psd1"; DestDir: "{#PS7Dir}\CredentialHandler"; Flags: ignoreversion
Source: "{#SourcePath}\modules\CredentialHandler\en-US\*"; DestDir: "{#PS7Dir}\CredentialHandler\en-US"; Flags: ignoreversion recursesubdirs createallsubdirs

; NOTE: Don't use "Flags: ignoreversion" on any shared system files


[Code]
function ShouldSkipPage(PageID: Integer): Boolean;
begin
  Result := False;

  if WizardSilent then
  begin
    case PageID of
      wpWelcome,
      wpLicense,
      wpInfoBefore,
      wpInfoAfter,
      wpSelectDir,
      wpReady,
      wpFinished:
        Result := True;
    end;
  end;
end;

function InitializeSetup(): Boolean;
var
  Param: string;
begin
  Param := GetCmdTail;

  if (Pos('--help', LowerCase(Param)) > 0) then
  begin
    MsgBox(
      'Custom help' + #13#10#13#10#13#10 +
      '--help' + #9#9 + 'Show this help message' + #13#10 +
      '/help, /?' + #9#9 + 'Show full advanced help' + #13#10 +
      '' + #9#9 + '(most flags will do nothing)' + #13#10 +
      '/silent' + #9#9 + 'Minimal UI, only shows progress and errors' + #13#10 +
      '/verysilent' + #9 + 'No UI at all, completely silent install' + #13#10 +
      '/LOG' + #9#9 + 'Causes Setup to create a' + #13#10 +
      ''+#9#9+'Log file in the Users %Temp% Directory' + #13#10 +
      '/LOG=filename' + #9 + 'Same as /LOG but specifies a' + #13#10 +
      ''+#9#9+'Filename/FullPath' + #13#10 +
      '/norestart' + #9 + 'Prevents automatic system restart after' + #13#10 +
      ''+#9#9+'install (currently not needed)',
      mbInformation, MB_OK
    );
    Result := False;
    Exit;
  end;

  Result := True;
end;